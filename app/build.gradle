apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'jacoco-android'

jacoco {
    toolVersion = "0.7.9"
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"
    defaultConfig {
        applicationId "com.alessandrocandolini.currency_converter"
        minSdkVersion 17
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        multiDexEnabled false
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {

        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            testCoverageEnabled true
        }

        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            testCoverageEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

    }


    testOptions {
        // force to FALSE to be sure there is exception if you call android stuff without mocking
        unitTests.returnDefaultValues = false
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }

    lintOptions {
        htmlReport true
        xmlReport true
        showAll true
        abortOnError false
        warningsAsErrors true
        quiet false
        ignoreWarnings false
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    jacocoAndroidUnitTestReport {
        csv.enabled false
        html.enabled true
        xml.enabled true
        excludes += [ // exclude here classes like annotation processing generated classes
                      '**/*_MembersInjector.class',
                      '**/Dagger*Component.class',
                      '**/Dagger*Component$Builder.class',
                      '**/module/*Module.kt',
                      '**/*Module_*Factory.kt',
                      '**/*_*Factory.class'
        ]
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.google.code.findbugs' // conflicting with 3.0.1 of dagger2.11
    })
    compile "com.android.support:appcompat-v7:26.0.2"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    // Junit 4
    // https://mvnrepository.com/artifact/junit/junit
    testCompile group: 'junit', name: 'junit', version: '4.12'

    // Mockito
    // https://mvnrepository.com/artifact/org.mockito/mockito-core
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.9.0'

    // Kotlin
    // https://mvnrepository.com/artifact/org.jetbrains.kotlin/kotlin-stdlib-jre7
    compile group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jre7', version: "$kotlin_version"

    // Dagger 2
    // https://mvnrepository.com/artifact/com.google.dagger/dagger
    def DAGGER_VERSION = '2.11'
    compile group: 'com.google.dagger', name: 'dagger', version: "$DAGGER_VERSION"
    kapt group: 'com.google.dagger', name: 'dagger-compiler', version: "$DAGGER_VERSION"
    compile group: 'com.google.dagger', name: 'dagger-android-support', version: "$DAGGER_VERSION"
    kapt group: 'com.google.dagger', name: 'dagger-android-processor', version: "$DAGGER_VERSION"



}
repositories {
    mavenCentral()
}
